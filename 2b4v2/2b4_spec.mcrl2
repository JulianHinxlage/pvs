sort
	CardinalDirection = struct north | east | south | west;
	Colour = struct red | yellow | green | none;

map
	next: Colour -> Colour;

eqn
	next(none) = red;
	next(red) = green;
	next(green) = yellow;
	next(yellow) = red;

act
	show: CardinalDirection # Colour;
	changeActive;
	changePhase;
	changeActiveAll;
	changePhaseBoth;

proc
	TrafficLight(d: CardinalDirection) =
		(d == north || d == south) 
			-> TrafficLight(d, none)
			<> changeActive.TrafficLight(d, none);


	TrafficLight(d : CardinalDirection,c : Colour) = 
		show(d, next(c)).(
			(next(c) == red)
				-> changeActive.changeActive.TrafficLight(d, next(c))
				<> changePhase.TrafficLight(d, next(c))
		);


	Crossing =
		hide(
			{
				changeActiveAll,
				changePhaseBoth
			},
		allow(
			{
				show,
				changeActiveAll,
				changePhaseBoth
			},
		comm(
			{
				changeActive|changeActive|changeActive|changeActive -> changeActiveAll,
				changePhase|changePhase -> changePhaseBoth
			},
			TrafficLight(west) ||
			TrafficLight(south)||
			TrafficLight(east) ||
			TrafficLight(north)
		)));

init
	Crossing;